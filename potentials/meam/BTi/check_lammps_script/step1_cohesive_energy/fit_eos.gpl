#!/usr/local/bin/gnuplot -persist
#------------------------------------------------
set terminal win font "Arial,12"
#------------------------------------------------
#set key opaque box lc rgb "white" height 1
set key box lw 1 lc "black"
set key top center
#-----------------------------------------------
set title "Murnaghan equation of state"
set xlabel "Volume [Angstrom^3]"
set ylabel "Cohesive energy [eV/atom]"
set mxtics 5
set mytics 5
#-----------------------------------------------
# Murnaghan equation of state
f(x) = (a*x/(b*(b-1)))*(b*(1-c/x)+(c/x)**b-1)+d
#
#Birch-Murnaghan equation of state (failed: BREAK: Singular matrix in Invert_RtR)
#f(x) = (9*c*a/16)*(((c/x)**(2/3)-1)**3*b+((c/x)**(2/3)-1)**2*(6-4*(c/x)**(2/3)))+d
#
# Vinet equation of state (failed: BREAK: Singular matrix in Invert_RtR)
#f(x) = (9*a*c/(b-1)**2)*(1+(b-1)*(1-(x/c)**(1/3))-exp(-(3/2)*(b-1)*(1-(x/c)**(1/3))))+d
#-----------------------------------------------
# new version [eV] and [Angstrom^3] unit
a=1.0       # Bulk modulus [eV/Angstrom^3] (1 [eV/A^3] = 160.2 [GPa])
b=7.3       # Differential of bulk modulus [dimensionless]
c=23.3      # Unit cell volume [Angstrom^3]
d=-16.7     # Cohesive energy in the volume of a stable unit cell [eV]
#-----------------------------------------------
fit f(x) 'lattice_cohesive_energy.txt' u 2:3 via a,b,c,d 
plot f(x) t "Murnaghan equation of state, f(x)", 'lattice_cohesive_energy.txt' u 2:3 t "MEAM (Lammps)" w p pt 1 ps 2
#------------------------------------------------
set terminal pngcairo color enhanced font "Arial" fontscale 1.0
set output "eos.png"
replot
#-----------------------------------------------
